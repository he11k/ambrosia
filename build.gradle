plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "org.jetbrains.kotlin.jvm" version "2.1.21"
	id "com.github.node-gradle.node" version "7.1.0"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = "Jitpack"
		url = uri("https://jitpack.io")
	}
	mavenCentral()
	maven { url = uri("https://cache-redirector.jetbrains.com/intellij-dependencies") }
}
fabricApi {
	configureDataGeneration {
		client = true
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	implementation "net.jodah:typetools:0.6.3"

	implementation 'org.jetbrains.intellij.deps.jcef:jcef:122.1.9-gd14e051-chromium-122.0.6261.94-api-1.18-251-b27'

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 21
	}
}

tasks.register('npmInstallSource', NpmTask) {
	workingDir = file("${project.rootDir}/src-theme")
	args = ["install"]
}

tasks.register('viteBuild', NpmTask) {
	dependsOn npmInstallSource
	workingDir = file("${project.rootDir}/src-theme")
	args = ["run", "build"]
	outputs.dir file("${project.rootDir}/src-theme/dist")
}

tasks.register('copyWeb', Copy) {
	dependsOn viteBuild
	from("${project.rootDir}/src-theme/dist")
	into("${projectDir}/src/main/resources/assets/ambrosia/browser")
}


sourceSets {
	main {
		resources {
			srcDirs "src-theme/resources"
		}
	}
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}